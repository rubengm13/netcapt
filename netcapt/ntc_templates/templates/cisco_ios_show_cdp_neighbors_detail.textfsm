Value Required DESTINATION_HOST (\S+)
Value SYSNAME (.*)
Value PLATFORM (.*)
Value SOFTWARE_VERSION (.*)
Value CAPABILITIES (.+?)
Value LOCAL_PORT (.*)
Value List MANAGEMENT_IP (.*)
Value REMOTE_PORT (.*)
Value List INTERFACE_IP (.*)

Start
  ^Device ID: ${DESTINATION_HOST}
  ^System Name: ${SYSNAME}
  ^Entry address\(es\)\s*:\s* -> ParseIP
  ^Platform\s*:\s*${PLATFORM}\s*,\s*Capabilities\s*:\s*${CAPABILITIES}\s*$$
  ^Interface: ${LOCAL_PORT},  Port ID \(outgoing port\): ${REMOTE_PORT}
  ^Version : -> GetVersion
  # Capture time-stamp if vty line has command time-stamping turned on
  ^Load\s+for\s+
  ^Time\s+source\s+is
  ^Management address\(es\): -> GetMgmtIP
  ^----------- -> Record

ParseIP
  ^.*IP.+[aA]ddress: ${INTERFACE_IP}
  ^Platform\s*:\s*${PLATFORM}\s*,\s*Capabilities\s*:\s*${CAPABILITIES}\s*$$ -> Start
  ^.* -> Start

GetVersion
  ^${SOFTWARE_VERSION} -> Start

GetMgmtIP
  ^.*IP.+[aA]ddress: ${MANAGEMENT_IP}
  ^----------- -> Record Start
  ^\s*$$
  ^\s*Total
  ^.* -> Error