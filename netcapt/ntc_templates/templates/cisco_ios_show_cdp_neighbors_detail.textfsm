Value LOCAL_PORT (.*)
Value Required DESTINATION_HOST (\S+)
Value REMOTE_PORT (.*)
Value List INTERFACE_IP (\d+\.\d+\.\d+\.\d+|\w+\.\w+\.\w+)
Value MANAGEMENT_IP (\d+\.\d+\.\d+\.\d+|\w+\.\w+\.\w+)
Value PLATFORM (.*)
Value SOFTWARE_VERSION (.*$)
Value CAPABILITIES (.+?)

Start
  ^Device ID: ${DESTINATION_HOST}
  ^Entry address\(es\)\s*:\s* -> ParseIP
  ^Platform\s*:\s*${PLATFORM}\s*,\s*Capabilities\s*:\s*${CAPABILITIES}\s+$$
  ^Platform\s*:\s*${PLATFORM}\s*,\s*Capabilities\s*:\s*${CAPABILITIES}$$
  ^Interface: ${LOCAL_PORT},  Port ID \(outgoing port\): ${REMOTE_PORT}
  ^Version : -> GetVersion
  # Capture time-stamp if vty line has command time-stamping turned on
  ^Load\s+for\s+
  ^Time\s+source\s+is
  ^Management address\(es\): -> GetMgmtIP
  ^----------- -> Record
  ^Total -> Record

ParseIP
  ^.*IP address: ${INTERFACE_IP}
  ^Platform\s*:\s*${PLATFORM}\s*,\s*Capabilities\s*:\s*${CAPABILITIES}\s+$$ -> Start
  ^Platform\s*:\s*${PLATFORM}\s*,\s*Capabilities\s*:\s*${CAPABILITIES}$$ -> Start
  ^.* -> Start


GetVersion
  ^${SOFTWARE_VERSION} -> Start

GetMgmtIP
  ^.*IP.+address: ${MANAGEMENT_IP} -> Start
